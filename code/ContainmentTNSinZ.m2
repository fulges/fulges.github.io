--ContainmentTNSinZ.m2

-- the script verifies the containment of the tensor network variety TNS(C3,(2,2,2),nn) 
-- in the variety Z_(n2,n3,2) described in the proof of the Theorem.

d = 3
m = 2
nn = (2,4,4)

-- set up of the vector spaces involved, regarded as degree 1 components of polynomial rings
WWW = QQ; -- W spaces
VVV = QQ; -- V spaces
HHH = QQ; -- Hom spaces
ALL = QQ;
for i from 1 to d do (
    V_i = QQ[v_(i,0)..v_(i,nn_(i-1)-1)];
    W_i = QQ[w_(i,0,0)..w_(i,m-1,m-1)];
    H_i = QQ[h_(i,0,(0,0))..h_(i,nn_(i-1)-1,(m-1,m-1))];
    VVV = VVV ** V_i;
    WWW = WWW ** W_i;
    HHH = HHH ** H_i;
    ALL = ALL ** W_i **V_i ** H_i)
use(ALL)

-- normalization described in the proof
-- the homomorphism X3 is entirely fixed by using the GL(V3) action
-- the first matrix describing the homomorphism X1 is normalized to be a rank one matrix with a single nonzero entries using the GL(V1) and the gauge action 
normalization = flatten flatten apply(2,i-> apply(2,j-> apply(4,k-> (
		if k == 2*i+j then (
		    h_(3,k,(i,j)) => 1) else h_(3,k,(i,j)) => 0)))) |
    	    	delete(null, flatten apply(2,i-> apply(2,j-> (
    				if ((i,j)!= (0,0)) then h_(1,0,(i,j)) => 0))))

-- define the endomorphisms matrices
toList apply(1..d, k-> (ww_k =matrix apply(2,i-> apply(2,j-> sum(nn_(k-1),p-> (
		    print(nn_(k-1),i,j,p);
		    h_(k, p, (i,j))*v_(k,p)))))))

-- define the image of the graph tensor via the fixed endomorphisms
T = sub(trace product(1..d, i-> ww_i),normalization)

-- the image of the first flattening regarded as a n2xn3 matrix
semiflat = diff(sub(basis({0,0,1},VVV),ALL),transpose(diff(sub(basis({0,1,0},VVV),ALL),T)))

-- its ideal of 3x3 minors:
-- it is an ideal generated by binary forms whose coefficients depend on the free variables of the homomorphisms
F = minors(3,semiflat);
gF= mingens F;

-- independently of the value of the free variables, we find two factors which are distinct and nonzero generically
fac0 = v_(1,1)
fac1 = (apply(toList factor (gF_(0,0)), ff -> value(ff)))_2

-- we verify that the binary forms are always divisible by these factors
gF % fac0
gF % fac1
